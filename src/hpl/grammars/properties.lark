// SPDX-License-Identifier: MIT
// Copyright © 2021 André Santos

hpl_property: [metadata] _scope ":" _pattern

metadata: _metadata_items

_metadata_items: _metadata_items? "#" _metadata_item

_metadata_item: metadata_id
              | metadata_title
              | metadata_desc

metadata_id: "id" ":" CNAME

metadata_title: "title" ":" ESCAPED_STRING

metadata_desc: "description" ":" ESCAPED_STRING

_scope: global_scope
      | after_until
      | until

global_scope: _KW_GLOBALLY

after_until: _KW_AFTER _activator [_KW_UNTIL _terminator]

until: _KW_UNTIL _terminator

_activator: _any_event

_terminator: _any_event

_pattern: existence
        | absence
        | response
        | prevention
        | requirement

existence: _KW_SOME _any_event _time_bound

absence: _KW_NO _any_event _time_bound

response: _any_event _KW_CAUSES _any_event _time_bound

prevention: _any_event _KW_FORBIDS _any_event _time_bound

requirement: _any_event _KW_REQUIRES _any_event _time_bound

_time_bound: [_KW_WITHIN time_amount]

_any_event: event
          | event_disjunction

event: channel_name [alias] [hpl_predicate]

event_disjunction: "(" (event _KW_OR)+ event ")"

time_amount: NUMBER TIME_UNIT

frequency: NUMBER FREQ_UNIT

alias: _KW_AS CNAME
