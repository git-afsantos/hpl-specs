// SPDX-License-Identifier: MIT
// Copyright © 2021 André Santos

channel_name: CHANNEL_NAME

int_literal: INT
string: ESCAPED_STRING
number: NUMBER
signed_number: SIGNED_NUMBER
boolean: TRUE | FALSE

TRUE: "True"
FALSE: "False"

RELATIONAL_OPERATOR: EQ_OPERATOR | NEQ_OPERATOR | COMP_OPERATOR | IN_OPERATOR
EQ_OPERATOR: "="
NEQ_OPERATOR: "!="
COMP_OPERATOR: /<=?/ | />=?/
IN_OPERATOR.2: "in"

NOT_OPERATOR.3: "not"
IF_OPERATOR.3: IMPLIES_OPERATOR | IFF_OPERATOR
IMPLIES_OPERATOR.3: "implies"
IFF_OPERATOR.3: "iff"
OR_OPERATOR.3: "or"
AND_OPERATOR.3: "and"

QUANT_OPERATOR.4: ALL_OPERATOR | SOME_OPERATOR
ALL_OPERATOR.4: "forall"
SOME_OPERATOR.4: "exists"

CONSTANT.5: "PI" | "INF" | "NAN" | "E"
ADD_OPERATOR: "+" | "-"
MULT_OPERATOR: "*" | "/"
POWER_OPERATOR: "**"
MINUS_OPERATOR: "-"

L_RANGE_EXC: "!["
L_RANGE_INC: "["
R_RANGE_EXC: "]!"
R_RANGE_INC: "]"

_KW_TO.4: "to"
_KW_IN.4: "in"
_KW_AS.4: "as"
_KW_OR.4: "or"
_KW_WITHIN.4: "within"
_KW_NO.4: "no"
_KW_SOME.4: "some"
_KW_REQUIRES.4: "requires"
_KW_CAUSES.4: "causes"
_KW_FORBIDS.4: "forbids"
_KW_AFTER.4: "after"
_KW_UNTIL.4: "until"
_KW_GLOBALLY.4: "globally"

CHANNEL_NAME: /[\/~]?[a-zA-Z][0-9a-zA-Z_]*(\/[a-zA-Z][0-9a-zA-Z_]*)*/

VAR_REF: "@" CNAME

TIME_UNIT: "s" | "ms"
FREQ_UNIT: "hz"

%import common.CNAME
%import common.INT
%import common.NUMBER
%import common.SIGNED_NUMBER
%import common.ESCAPED_STRING
%import common.WS
%ignore WS
